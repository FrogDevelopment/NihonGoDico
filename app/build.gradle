apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.ajoberstar.grgit'

def getVersionCode = { ->
    return grgit.tag.list().size()
}

def getVersionName = { ->
    return grgit.describe()
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    defaultConfig {
        applicationId 'fr.frogdevelopment.nihongo.dico'

        minSdkVersion 26
        targetSdkVersion 30

        versionCode getVersionCode()
        versionName getVersionName()
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        viewBinding = true
        dataBinding = true
    }
}

dependencies {
    // Room components
    implementation 'androidx.room:room-runtime:2.3.0'
    implementation 'androidx.room:room-ktx:2.3.0'
    kapt 'androidx.room:room-compiler:2.3.0'
    androidTestImplementation 'androidx.room:room-testing:2.3.0'

    // Lifecycle components
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    kapt 'androidx.lifecycle:lifecycle-common-java8:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'

    // kotlin
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.5.0'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.12.3'

    // UI
    def appcompat_version = '1.2.0'
    def constraint_layout_version = '2.0.4'
    def preferenceVersion = '1.1.1'
    def material_version = '1.3.0'
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout_version"
    implementation "androidx.preference:preference-ktx:$preferenceVersion"
    implementation "com.google.android.material:material:$material_version"
    implementation 'com.github.ybq:Android-SpinKit:1.4.0'

    // Testing
    def coreTestingVersion = '2.1.0'
    def junitVersion = '4.13.2'
    androidTestImplementation "androidx.arch.core:core-testing:$coreTestingVersion"
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:3.10.0"

    // Divers
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.commons:commons-csv:1.8'
    implementation 'org.apache.commons:commons-compress:1.20'
}

task printVersion() {
    doLast {
        println("Version Code: ${getVersionCode()}")
        println("Version Name: ${getVersionName()}")
    }
}
repositories {
    mavenCentral()
}
